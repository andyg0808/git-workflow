#!/bin/sed -nrf
# summarize-bash-script: a sed script to generate fake docs from bash
# Copyright (C) 2013  Andrew Gilbert
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.


# If it's a hash (comment at column 0), process it.
/^#/b hash

# If it's a function, process it.
/\(\)\s+\{$/b func

# If it's a blank line, invalidate the current collection of hash
/^$/b blank

# Otherwise, delete it.
d

:hash
# Put four spaces at the beginning of each line
#s/^#/#    /

# If it's the hashbang, get rid of it
/#!/d

# Append the pattern space to the hold space with newline
H

# Done. Go to end.
b

:blank
# Clear pattern space
z

# Swap that into hold space (clearing hold space)
x

# Done. Go to end.
b

:func
# Process the function:
# Put a bracket to close the function
s/$/\}/

# Print the function name
p

# Print vim fold marker
i\
#{{{

# Print the close fold marker with a trailing newline at the end of this cycle
a\
#}}}\


# Replace the function with the hashes
g

# Remove the initial newline on the hashes
s/^\n//

# Append a newline to the hashes
#s/$/\n/

# Print hashes
p

# Clear pattern space
z

# Swap that into hold space (clearing hold space)
x
